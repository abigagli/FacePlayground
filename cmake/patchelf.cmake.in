GET_FILENAME_COMPONENT (ABSOLUTE_INSTALL_PREFIX ${CMAKE_INSTALL_PREFIX} REALPATH)
SET (DEPENDENCY_DESTDIR ${ABSOLUTE_INSTALL_PREFIX}/@DEPENDENCIES_INSTALL_DIR@)

GET_FILENAME_COMPONENT (TARGET_FILENAME $<TARGET_FILE:@TARGET@> NAME)
SET (TARGET_INSTALLED_PATH ${ABSOLUTE_INSTALL_PREFIX}/@TARGET_INSTALL_DIR@/${TARGET_FILENAME})

#NOTE:***   CACHING THE RESULT OF GET_FILENAME_COMPONENT HERE CAUSES INSTALLING THE INTERPRETER ONLY ONCE
#           WHICH OF COURSE WOULD BE THE WRONG THING IN THE UNLINKELY CASE THAT DIFFERENT EXECUTABLES USE DIFFERENT
#           INTERPRETERS
IF (NOT ORIGINAL_INTERPRETER_ABSOLUTE_PATH)
    #MESSAGE ("FINDING ORIGINAL INTERPRETER FOR ${TARGET_INSTALLED_PATH}")
    EXECUTE_PROCESS (COMMAND @ELF_PATCHER@ --print-interpreter ${TARGET_INSTALLED_PATH} OUTPUT_VARIABLE ORIGINAL_INTERPRETER OUTPUT_STRIP_TRAILING_WHITESPACE)

    GET_FILENAME_COMPONENT (ORIGINAL_INTERPRETER_ABSOLUTE_PATH ${ORIGINAL_INTERPRETER} REALPATH CACHE)

    FILE ( INSTALL
        ${ORIGINAL_INTERPRETER_ABSOLUTE_PATH}
        DESTINATION ${DEPENDENCY_DESTDIR}
        TYPE EXECUTABLE
        )
ENDIF()

GET_FILENAME_COMPONENT (ORIGINAL_INTERPRETER_FILENAME ${ORIGINAL_INTERPRETER_ABSOLUTE_PATH} NAME CACHE)

SET (NEW_INTERPRETER_ABSPATH @ELF_INTERPRETER_ABSPATH@)

IF (NOT NEW_INTERPRETER_ABSPATH)
    SET (NEW_INTERPRETER_ABSPATH ${DEPENDENCY_DESTDIR}/${ORIGINAL_INTERPRETER_FILENAME})
ENDIF()

IF (NOT EXISTS ${NEW_INTERPRETER_ABSPATH})
    MESSAGE (WARNING "*** ELF-PATCHING ${TARGET_INSTALL_DIR_PATH} to use NON-EXISTING ${NEW_INTERPRETER_ABSPATH} ***")
ELSE()
    MESSAGE (STATUS "*** ELF-PATCHING ${TARGET_INSTALLED_PATH} to use ${NEW_INTERPRETER_ABSPATH} ***")
ENDIF()

EXECUTE_PROCESS (COMMAND @ELF_PATCHER@ --set-interpreter ${NEW_INTERPRETER_ABSPATH} ${TARGET_INSTALLED_PATH} RESULT_VARIABLE ELFPATCH_FAILED)

IF (ELFPATCH_FAILED)
    MESSAGE (FATAL_ERROR "!!!!! COULDN'T CHANGE INTERPRETER OF ${TARGET_INSTALLED_PATH} TO ${NEW_INTERPRETER_ABSPATH} !!!!!")
ENDIF()


